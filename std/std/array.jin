import std.intrinsics
import std.option.{Option}

type Array*[T](buf: [T])

fn Array.new*[T]() -> Array[T] = Array([])

fn Array.new*[T](len: uint, f: fn(uint) -> T) -> Array[T] = {
	let mut array = Array.with_cap(len)
	let mut i = 0

	for if i < len {
		array.push(f(i))
		i += 1
	}

	array
}

fn Array.with_cap*[T](cap: uint) -> Array[T] = Array([:cap])

fn len*[T](self: &Array[T]) -> uint = self.buf.len
fn cap*[T](self: &Array[T]) -> uint = self.buf.cap
fn is_empty*[T](self: &Array[T]) -> bool = self.len() == 0

fn as_ref*[T](self: &Array[T]) -> &[T] = &self.buf
fn as_mut*[T](self: &mut Array[T]) -> &mut [T] = &mut self.buf
fn take*[T](self: Array[T]) -> [T] = self.buf

fn get*[T](self: &Array[T], index: uint) -> Option[&T] = {
	if index < self.buf.len {
		Option.some(&self.buf.[index])
	} else {
		Option.none
	}
}

fn push*[T](self: &mut Array[T], value: T) = {
	if self.buf.len == self.buf.cap {
		self.grow()
	}
	self.buf.len += 1
	self.buf.[self.buf.len - 1] = value
}

fn pop*[T](self: &mut Array[T]) -> Option[T] = {
	if self.buf.len == 0 {
		return Option.none
	}

	self.buf.len -= 1
	Option.some({self.buf.ptr + self.buf.len}.*)
}

fn clear*[T](self: &mut Array[T]) = {
	self.buf = []
}

fn grow[T](self: &mut Array[T]) = {
	let new_cap = if self.buf.cap == 0 {
		MIN_CAP
	} else {
		self.buf.cap * 2
	}

	let new_buf = intrinsics.grow(transmute[[T]](&mut self.buf), new_cap)
	let old = self.buf := new_buf
	intrinsics.forget(&old)
}

let MIN_CAP: uint = 4
