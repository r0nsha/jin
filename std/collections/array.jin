use std.builtin
use std.option.Option

pub type Array[T](buf: [T])

pub fn Array.new[T]() -> Array[T] = Array([])

pub fn Array.new[T](len: uint, f: fn(uint) -> T) -> Array[T] = {
	let mut array = Array.with_cap(len)
	let mut i = 0

	for if i < len {
		array.push(f(i))
		i += 1
	}

	array
}

pub fn Array.from[T](slice: [T]) -> Array[T] = Array(slice)

pub fn Array.with_cap[T](cap: uint) -> Array[T] = {
	# TODO: use a polymorphic `min` function for this	
	let c = if cap > min_cap { cap } else { min_cap }
	Array([:c])
}

pub fn len[T](self: &Array[T]) -> uint = self.buf.len
pub fn cap[T](self: &Array[T]) -> uint = self.buf.cap
pub fn is_empty[T](self: &Array[T]) -> bool = self.len() == 0

pub fn as_ref[T](self: &Array[T]) -> &[T] = &self.buf
pub fn as_mut[T](self: &mut Array[T]) -> &mut [T] = &mut self.buf
pub fn take[T](self: Array[T]) -> [T] = self.buf

pub fn get[T](self: &Array[T], index: uint) -> Option[&T] = {
	if index < self.buf.len {
		Option.some(&self.buf.[index])
	} else {
		Option.none
	}
}

pub fn set[T](self: &mut Array[T], index: uint, value: T) -> Option[T] = {
	if index < self.buf.len {
		Option.some(self.buf.[index] := value)
	} else {
		Option.none
	}
}

pub fn push[T](self: &mut Array[T], value: T) = {
	self.reserve(1)
	unsafe self.buf.len += 1
	self.buf.[self.buf.len - 1] = value
}

pub fn pop[T](self: &mut Array[T]) -> Option[T] = {
	if self.buf.len == 0 {
		return Option.none
	}

	unsafe self.buf.len -= 1
	Option.some(unsafe {self.buf.data + self.buf.len}.0)
}

pub fn clear[T](self: &mut Array[T]) = {
	self.buf = []
}

pub fn reserve[T](self: &mut Array[T], additional: uint) = {
	if self.needs_to_grow(additional) {
		self.grow()
	}
}

fn needs_to_grow[T](self: &Array[T], additional: uint) -> bool =
	# TODO: this is prone to overflow, we should implement checked arithmetic
	self.buf.len + additional > self.buf.cap


fn grow[T](self: &mut Array[T]) = {
	let new_cap = if self.buf.cap == 0 {
		min_cap
	} else {
		self.buf.cap * 2
	}

	let new_buf = unsafe builtin.grow(transmute[[T]](&mut self.buf), new_cap)
	let old = self.buf := new_buf
	unsafe builtin.forget(&old)
}

let min_cap: uint = 4
